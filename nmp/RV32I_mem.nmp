/*
 *	RISC-V Instruction Set
 *
 *	This file is part of GLISS
 *	Copyright (c) 2017, IRIT UPS.
 *
 *	GLISS is free software; you can redistribute it and/or modify
 *	it under the terms of the GNU General Public License as published by
 *	the Free Software Foundation; either version 2 of the License, or
 *	(at your option) any later version.
 *
 *	GLISS is distributed in the hope that it will be useful,
 *	but WITHOUT ANY WARRANTY; without even the implied warranty of
 *	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *	GNU General Public License for more details.
 *
 *	You should have received a copy of the GNU General Public License
 *	along with GLISS; if not, write to the Free Software
 *	Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 */
  

// load instructions
op all_loads_32 =
	  lb
	| lbu
	| lh
	| lhu
	| lw
	
// store instructions
op all_stores_32 =
	  sb
	| sh
	| sw

// load instructions
op lb(offset: int(12), base: reg_t, dest: reg_t)
	syntax = format("lb %s, %d(%s)", dest, offset, base)
	image  = format("%12b %s 000 %s 0000011", offset, base, dest)
	action = {
		let vAddr = R[base] + offset;
		setR(dest, coerce(word, M[vAddr]));
	}

op lbu(offset: int(12), base: reg_t, dest: reg_t) //DONE
	syntax = format("lbu %s, %d(%s)", dest, offset, base)
	image  = format("%12b %s 100 %s 0000011", offset, base, dest)
	action = {
		let vAddr = R[base] + offset;
		setR(dest, coerce(uword, M[vAddr]));
	}

op lh(offset: int(12), base: reg_t, dest: reg_t)
	syntax = format("lh %s, %d(%s)", dest, offset, base)
	image  = format("%12b %s 001 %s 0000011", offset, base, dest)
	action = {
		let vAddr = offset + R[base];
		setR(dest, coerce(word,M16[vAddr]));
	}

op lhu(offset: int(12), base: reg_t, dest: reg_t) //DONE
	syntax = format("lhu %s, %d(%s)", dest, offset, base)
	image  = format("%12b %s 101 %s 0000011", offset, base, dest)
	action = {
		let vAddr = offset + R[base];
		setR(dest, coerce(uword,M16[vAddr]));
	}

op lw(offset: int(12), base: reg_t, dest: reg_t) //DONE
	syntax = format("lw %s, %d(%s)", dest, offset, base)
	image  = format("%12b %s 010 %s 0000011", offset, base, dest)
	action = {
		let vAddr = offset + R[base];
		setR(dest, coerce(word,M32[vAddr]));
	}
	

// store instructions
op sb(offset: int(12), src: reg_t, base: reg_t) //DONE
	syntax = format("sb %s, %d(%s)", src, offset, base)
	image  = format("%7b %s %s 000 %5b 0100011", offset<11..5>, src, base, offset<4..0>)
	action = {
		let vAddr = offset + R[base];
		let databyte = R[src]<7..0>;
		/* DEBUG */
		//"printf"("M[%08X]: %08X\n",vAddr,databyte);
		/*********/
		M[vAddr] = databyte;
	}

op sh(offset: int(12), src: reg_t, base: reg_t) //DONE
	syntax = format("sh %s, %d(%s)", src, offset, base)
	image  = format("%7b %s %s 001 %5b 0100011", offset<11..5>, src, base, offset<4..0>)
	action = {
		let vAddr = offset + R[base];
		let databyte = R[src]<15..0>;
		M16[vAddr] = databyte;
	}

op sw(offset: int(12), src: reg_t, base: reg_t) //DONE
	syntax = format("sw %s, %d(%s)", src, offset, base)
	image  = format("%7b %s %s 010 %5b 0100011", offset<11..5>, src, base, offset<4..0>)
	action = {
		let vAddr = offset + R[base];
		let dataword = R[src]<31..0>;
		M32[vAddr] = coerce(word,dataword);
	}

